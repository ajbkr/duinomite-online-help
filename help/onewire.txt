NAME
  ONEWIRE -- One Wire

DESCRIPTION
  Many thanks to Mr. Gerard Sexton for writing the
  "One Wire" command set for the Duinomite.

  There are now 4 commands and 2 functions:

  OWRESET pin [,presence]
  OWWRITE pin, flag, length, data [, data]
  OWREAD pin, flag, length, data [, data]
  OWSEARCH pin, srchflag, ser [,ser]

  The OWRESET and OWSEARCH commands (and the
  OWREAD and OWWRITE commands if a reset is
  requested) set the MM.OW variable:

    1 = OK (presence detected, search successful)
    else 0 = Fail (presence not detected, search
    unsuccessful).

  Where:
    pin - the MMBasic I/O pin to use

    presence - an optional variable to receive the
    presence pulse (1 = device response, 0 = no
    device response)

    flag - a combination of the following options:

      1 - send reset before command
      2 - send reset after command
      4 - only send/recv a bit instead of a byte
      of data
      8 - invoke a strong pullup after the command
      (the pin will be set high and open drain
      disabled)

    length - length of data to send or receive

    data - data to send or receive

    srchflag - a combination of the following
    options:

      1 - start a new search
      2 - only return devices in alarm state
      4 - search for devices in the requested
      family (first byte of ser)
      8 - skip the current device family and
      return the next device
      16 - verify that the device with the serial
      number in ser is available
   
      If srchflag = 0 (or 2) then the search will
      return the next device found

    ser - serial number (8 bytes) will be returned
    (srchflag 4 and 16 will also use the values in
    ser)

      After the command is executed, the pin will
      be set to the NOT_CONFIGURED state unless
      flag option 8 is used.

    The data and ser arguments can be a string,
    array or a list of variables.

  OWCRC8(len, cdata [, cdata…])    Processes
  the cdata and returns the 8 bit CRC
  OWCRC16(len, cdata [, cdata…])   Processes
  the cdata and returns the 16 bit CRC

  Where:
    len - length of data to process

    cdata - data to process

      The cdata can be a string, array or a list
      of variables.

  An example of using the code to read the
  temperature (and all registers) from a DS18B20 
  (with external power) and search and verify the
  serial number:

  ================================================

EXAMPLE
  10  dim ar(10)
  20  p = 18
  30  owreset p,presence:? "Presence: ";presence;
  35  precision = 3 : gosub 500
  40  owwrite p,1,2,&hcc,&h44
  50  for i = 1 to 12000
  60   owread p,4,1,b
  70   if b = 1 then
  80    if (i mod 8) = 0 then exit for
  90   endif
  100 next
  120 owwrite p,1,2,&hcc,&hbe
  130 owread p,2,9,ar(0)
  140 ? ", Data: (";ar(0);",";ar(1);",";ar(2);",";
   ar(3);",";ar(4);",";ar(5);",";ar(6);",";ar(7);
   ",";ar(8);
  150 owreset p,presence:? " ), Presence: ";
   presence
  155 ? "CRC: ";owcrc8(9,ar(0))
  160 tl = ar(0) : th = ar(1)
  170 if (th and &h80) then
  190  temp = ((tl and &h0f) * 0.0625) + ((tl and
   &hf0) / 16) + ((th and &h07) * 16) - 128
  200 else
  220  temp = ((tl and &h0f) * 0.0625) + ((tl and
   &hf0) / 16) + ((th and &h07) * 16)
  230 endif
  240 tempf = ((temp * 9) / 5) + 32
  250 ? "Temp: ";temp;"C, ";tempf;"F"
  260 owsearch p,1,ar(0)
  262 if mm.ow = 0 then
  264  ? "Search failed"
  266 else
  270  do while mm.ow = 1
  280   ? "Serial number: (";ar(0);",";ar(1);",";
   ar(2);",";ar(3);",";ar(4);",";ar(5);",";ar(6);
   ",";ar(7);" )"
  290   owsearch p,0,ar(0)
  300  loop
  310 endif
  320 owsearch p,16,ar(0)
  330 if mm.ow = 1 then
  340  ? "Serial number: (";ar(0);",";ar(1);",";
   ar(2);",";ar(3);",";ar(4);",";ar(5);",";ar(6);
   ",";ar(7);" )"
  350 else
  360  ? "Verify failed"
  370 endif
  390 end
  500 if precision = 4 then
  510  owwrite p,1,5,&hcc,&h4e,&h00,&h00,&h7f
  520 elseif precision = 3 then
  530  owwrite p,1,5,&hcc,&h4e,&h00,&h00,&h5f
  540 elseif precision = 2 then
  550  owwrite p,1,5,&hcc,&h4e,&h00,&h00,&h3f
  560 else
  570  owwrite p,1,5,&hcc,&h4e,&h00,&h00,&h1f
  580 endif
  590 return
